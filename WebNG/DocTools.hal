external function string 255 ToolWebNGtranslateText(integer);
external function string 255 FormatSimpleLink(string,boolean);
external procedure ShowRedirectHtml(string,boolean);

global
updating procedure SendMailForAttachments(var record SMFVc SMFr)
begin
  record MailVc Mailr;
  row MailVc Mailrw;
  integer i;
  string 100 tstr;
  record JALClassBlock JCbl;
  record MailSettingsBlock MSr;
  BlockLoad(MSr);

  BlockLoad(JCbl);
  RecordNew(Mailr);
  Mailr.Header = UsetStr(32354) & SMFr.SMFName;
  AddToText(USetStr(32355),Mailr);
  
  ClearRow(Mailr,Mailrw,1);
  Mailrw.RowTyp = kMailRowTypeFrom;
  //FindCurMailboxName(tstr);
 // Stopalert("rrr " & MSr.FromSystem);
  Mailrw.AddrCode = MSr.FromSystem;
  MatRowPut(Mailr,0,Mailrw);
  ClearRow(Mailr,Mailrw,1);
  Mailrw.RowTyp = kMailRowTypeTo;
  Mailrw.AddrCode = JCbl.AttachConf;
  MatRowPut(Mailr,1,Mailrw);
  Mailr.SendFlag = 1;
  if recordStore(Mailr,true) then begin
    //CreateRecordLink(SMFr,CurrentCompany,mailr,CurrentCompany);
	  CreateRecordLink(mailr,CurrentCompany,SMFr,CurrentCompany);
  end;
  return;
end;



procedure ShowDocumentFail()
begin
 
  WebOutStringFormatNL("<div class=""err"">" & ToolWebNGTranslateText(11111) & "</div>");

return;
end;

procedure ShowScoreSelectBox(string code,longint sernr,integer score)
begin
  integer i;
  record JALClassBlock JCbl;

  BlockLoad(JCbl);


  WebOutStringFormatNL("<select onchange='ChangeFileScore(this.options[this.selectedIndex].value,""" & code & """," & sernr & ",""" & WebGetArg("user") & """);'>");
  WebOutStringFormatNL("<option value='-1' selected>-</option>");  
  for (i = JCbl.ScoreFrom; i <=JCbl.ScoreTo; i = i + 1) begin
    if (score == i) then begin
      WebOutStringFormatNL("<option value='" & i & "' selected>" & i & "</option>");
    end else begin
      WebOutStringFormatNL("<option value='" & i & "'>" & i & "</option>");
    end;
  end;

  WebOutStringFormatNL("</select>");



return;
end;


function boolean LastStatusExists(longint sernr,integer type,var integer newstat)
begin
  boolean res;
  record JALFileHistVc JFHr;
  boolean TrHs;

  res = false;
  TrHs = true;
  JFHr.TransDate = AddYear(CurrentDate,1);
  JFHr.DocSerNr = sernr;
  ResetLoop(JFHr);
  while loopBackkey("DocDate",JFHr,2,TrHs) begin
    if JFHr.Status <> 3 then begin
      TrHs = false;
      switch type begin
        case 4: if JFHr.Status == 0 then begin newstat = 0; res = true; end;
        case 5: if JFHr.Status == 1 then begin newstat = 1; res = true; end;
      end;
    end;
  end;


  LastStatusExists = res;
return;
end;


procedure ShowDocStatus(string code,integer status,var integer cnt,integer type,string link,boolean showf)
begin
  record JALFileVc JFr;
  record Attach2Vc Attachr;
  record RLinkVc RLr;
  integer i;
  boolean TrHs;
  string 255 fn;
  integer newstat;

  ResetLoop(JFr);
  JFr.SMFCode = code;
  JFr.Status = status;
  if status == 0 then begin
     WebOutStringFormatNL("<div class=""doc_col last"">");
  end else begin
    WebOutStringFormatNL("<div class=""doc_col"">");
  end;
  WebOutStringFormatNL("<div class=""doc_col_header"">");
  WebOutStringFormatNL(ToolWebNGTranslateText(20300+status));
  WebOutStringFormatNL("</div>");
  if showf then begin
    WebOutStringFormatNL("<div class=""doc_col_labels"">");
    WebOutStringFormatNL("<div class=""doc_col_lbl"">");
    WebOutStringFormatNL(ToolWebNGTranslateText(20310));
    WebOutStringFormatNL("</div>");
    WebOutStringFormatNL("<div class=""doc_col_lbl"">");
    WebOutStringFormatNL(ToolWebNGTranslateText(20311));
    WebOutStringFormatNL("</div>");
    WebOutStringFormatNL("</div>");
  end;
  TrHs = true;
  while loopkey("SMFCode",JFr,2,TrHs) begin
    if (JFr.SMFCode <> code or JFr.Status <> status) then begin
      TrHs = false;
    end else begin
      i = 1;
      while readrecordlink(JFr,i,Attachr,RLr) begin
        //WebOutStringFormatNL("<a href=""#"" onclick=""window.open('http://localhost:8191/WebOutImg.hal?sernr=" & Attachr.SerNr & "','','');"">" & Attachr.FileName & "</a>");
        WebOutStringFormatNL("<div class=""file_row"">");
        fn = Attachr.FileName;
        if status == 2 then begin
          if (JFr.Score == -1) then begin
            fn = fn & " (-)";
          end else begin
            fn = fn & " ("& JFr.Score & ")";
          end;
        end;
        switch type begin
          case 0: WebOutStringFormatNL("<a href=""/WebOutImg.hal?sernr=" & Attachr.SerNr & """ target=_blank title=""" & fn & """>" & fn & "</a>");       
          case 1: WebOutStringFormatNL("<a class=""short"" href=""/WebOutImg.hal?sernr=" & Attachr.SerNr & """ target=_blank title=""" & fn & """>" & fn & "</a>");       
                  WebOutStringFormatNL("<div class=""approve_but"">");
                  WebOutStringFormatNL("<a class=""approve_up"" href=""/WebChangeFileStatus.hal?file=" & JFr.SerNr & "&stat=" & (status+1) & "&tpath=" & link & "&user=" & WebGetArg("user") & """></a>");
                  WebOutStringFormatNL("<a class=""approve_down"" href=""/WebChangeFileStatus.hal?file=" & JFr.SerNr & "&stat=3&tpath=" & link & "&user=" & WebGetArg("user") & """></a>");
                  WebOutStringFormatNL("</div>");
          case 2: WebOutStringFormatNL("<a class=""short"" href=""/WebOutImg.hal?sernr=" & Attachr.SerNr & """ target=_blank title=""" & fn & """>" & fn & "</a>");       
                  WebOutStringFormatNL("<div class=""approve_but"">");
                  WebOutStringFormatNL("<a class=""approve_back"" href=""/WebChangeFileStatus.hal?file=" & JFr.SerNr & "&stat=" & (status-1) & "&tpath=" & link & "&user=" & WebGetArg("user") & """></a>");
                  WebOutStringFormatNL("</div>");
          case 3: WebOutStringFormatNL("<a class=""short"" href=""/WebOutImg.hal?sernr=" & Attachr.SerNr & """ target=_blank title=""" & fn & """>" & fn & "</a>");       
                  WebOutStringFormatNL("<div class=""approve_but"">");
                  WebOutStringFormatNL("<div class=""score_select"">");
                  ShowScoreSelectBox(code,JFr.SerNr,JFr.Score);
                  WebOutStringFormatNL("</div>");    
                  
                  WebOutStringFormatNL("<a class=""approve_back"" href=""/WebChangeFileStatus.hal?file=" & JFr.SerNr & "&stat=" & (status-1) & "&tpath=" & link & "&user=" & WebGetArg("user") & """></a>");
                  WebOutStringFormatNL("</div>");
          otherwise
                  if (LastStatusExists(JFr.SerNr,type,newstat)) then begin
                    WebOutStringFormatNL("<a class=""short"" href=""/WebOutImg.hal?sernr=" & Attachr.SerNr & """ target=_blank title=""" & fn & """>" & fn & "</a>");       
                    WebOutStringFormatNL("<div class=""approve_but"">");
                    WebOutStringFormatNL("<a class=""approve_back"" href=""/WebChangeFileStatus.hal?file=" & JFr.SerNr & "&stat=" & (newstat) & "&tpath=" & link & "&user=" & WebGetArg("user") & """></a>");
                    WebOutStringFormatNL("</div>");
                  end else begin
                    WebOutStringFormatNL("<a href=""/WebOutImg.hal?sernr=" & Attachr.SerNr & """ target=_blank title=""" & fn & """>" & fn & "</a>"); 
                  end;
          
        end;      
        WebOutStringFormatNL("</div>");
        i = i + 1;
        cnt = cnt + 1;
      end;
    end;
  end;
  if cnt == 0 then begin
    ShowDocumentFail;
  end;
  cnt = 0;

  WebOutStringFormatNL("</div>");

return;
end;


global 
procedure ShowDocumentList(string code,integer type,string link)
begin
  integer cnt;

  

  WebOutStringFormatNL("<div id=""doc_box"">");
  cnt = 0;
  /*if type==1 then begin
    ShowDocStatus(code,0,cnt,true,link,true);
  end else begin
    ShowDocStatus(code,0,cnt,false,link,false);
  end;
  if type==0 then begin
    ShowDocStatus(code,1,cnt,true,link,false);
  end else begin
    ShowDocStatus(code,1,cnt,false,link,false);
  end;*/
  switch type begin
    case 0: ShowDocStatus(code,0,cnt,0,link,false);
            ShowDocStatus(code,1,cnt,1,link,false);
            ShowDocStatus(code,2,cnt,3,link,false);
            ShowDocStatus(code,3,cnt,5,link,false);
    case 1: ShowDocStatus(code,0,cnt,1,link,true);
            ShowDocStatus(code,1,cnt,2,link,false);
            ShowDocStatus(code,2,cnt,0,link,false);
            ShowDocStatus(code,3,cnt,4,link,false);
    case 2: ShowDocStatus(code,0,cnt,0,link,false);
            ShowDocStatus(code,1,cnt,0,link,false);
            ShowDocStatus(code,2,cnt,0,link,false);
            ShowDocStatus(code,3,cnt,0,link,false);
  end;
  
  WebOutStringFormatNL("</div>");

return;
end;


global webpublic
procedure WebShowDocuments()
begin
  record SMFVc SMFr;


  SMFr.SMFCode = WebGetArg("code");

  WebOutStringFormatNL("<html>");
  WebOutStringFormatNL("<head>");
  WebOutStringFormatNL("<title></title>");
  WebOutStringFormatNL("<link rel=""stylesheet"" type=""text/css"" href=""/doc.css"">");
  WebOutStringFormatNL("<script type=""text/javascript"" src=""/doc.js""></script>");
  WebOutStringFormatNL("</head>");
  WebOutStringFormatNL("<body>");

  if (nonblank(SMFr.SMFCode) and readfirstmain(SMFr,1,true)) then begin
    ShowDocumentList(SMFr.SMFCode,0,"/WebShowDocuments.hal&code=" & SMFr.SMFCode & "&user=" & WebGetArg("user"));
  end else begin
    ShowDocumentFail;
  end;

  WebOutStringFormatNL("</body>");
  WebOutStringFormatNL("</html>");


return;
end;



procedure SetContentTypeForExtension(string ext)
begin
  switch (ext) begin
    case "css" : WebSetContentType("text/css");
    case "jpg" : WebSetContentType("image/jpeg");
    case "jpeg" : WebSetContentType("image/jpeg");
    case "png" : WebSetContentType("image/png");
    case "gif" : WebSetContentType("image/gif");
    case "htc" : WebSetContentType("text/x-component");
    case "exe" : WebSetContentType("application/binary");
    case "bin" : WebSetContentType("application/binary");
    case "obj" : WebSetContentType("application/binary");
    case "hpf" : WebSetContentType("application/binary");
    case "hob" : WebSetContentType("application/binary");
    case "dat" : WebSetContentType("application/binary");
    case "swf" : WebSetContentType("application/x-shockwave-flash");
    case "pdf" : WebSetContentType("application/pdf");
    case "zip" : WebSetContentType("application/zip");
    case "txt" : WebSetContentType("text/plain");
    case "xml" : WebSetContentType("text/xml");
    case "xsl" : WebSetContentType("text/xsl");
    case "htm" : WebSetContentType("text/html");
    case "html" : WebSetContentType("text/html");
    otherwise
      WebSetContentType("application/binary");
  end;
  return;
end;

global webpublic
procedure WebOutImg()
begin
  record Attach2Vc Attachr;
  string 255 ext;
  Attachr.SerNr = StringToLongint(WebGetArg("sernr"));
  if ReadFirstMain(Attachr,1,true) then begin
    if (Attachr.SerNr>0) then begin
      ext = right(Attachr.FileName,4);
      SetContentTypeForExtension(ext);
      WebSetContentDisposition("inline; filename=" & Attachr.FileName);
      WebOutAttachment(Attachr.SerNr);
    end;
  end;
  
return;
end;



global webpublic
updating procedure WebChangeFileStatus()
begin
  record JALFileVc JFr,oldJFr;
  record CUVc CUr;
  record GlobalUserVc GUserr;
  record TeacherVc TCr;
  record SMFVc SMFr;
  integer status;
  string 255 link;
  integer rwcnt,i;
  row SMFVc SMFrw;

  link = "";

  JFr.SerNr = StringToLongInt(WebGetArg("file"));
  status = StringToInt(WebGetArg("stat"));

  CUr.Code = CurrentCust;
  GUserr.Code = WebGetArg("user");

  if (nonblank(CUr.Code)) then begin
    if readfirstmain(CUr,1,true) then begin
      TCr.CustCode = CUr.Code;
      if readfirstkey("CustCode",TCr,1,true) then begin
        if readfirstmain(JFr,1,true) then begin
          SMFr.SMFCode = JFr.SMFCode;
          if readfirstmain(SMFr,1,true) then begin
            //if TCr.TeacherCode == SMFr.TeacherCode then begin
            rwcnt = matrowcnt(SMFr);
            for (i = 0; i < rwcnt; i = i + 1) begin
              MatRowGet(SMFr,i,SMFrw);
              if SMFrw.Type == 0 and SMFrw.Code == TCr.TeacherCode then begin
                i = rwcnt;
                recordCopy(oldJFr,JFr);
                JFr.Status = status;
                if recordUpdate(oldJFr,JFr,true)==0 then begin end;
                link = WebGetArg("tpath");
                //jasuta epasts
                if status <> 3 then begin
                  SendMailForAttachments(SMFr);
                end;
              end;
            end;
            //end;
          end;
        end;
      end;
    end;
  end else begin
    
    if readfirstmain(GUserr,1,true) then begin
      if readfirstmain(JFr,1,true) then begin
        recordCopy(oldJFr,JFr);
        JFr.Status = status;
        if recordUpdate(oldJFr,JFr,true)==0 then begin end;
        link = WebGetArg("tpath") & "?code=" & WebGetArg("code") & "&user=" & WebGetArg("user");
      end;
    end;
  end;

  if nonblank(link) then begin
    ShowRedirectHtml(FormatSimpleLink(link,true),false);
  end else begin
    ShowRedirectHtml(FormatSimpleLink(WebGetArg("tpath"),true),false);
  end;

return;
end;

global webpublic 
updating procedure WebChangeFileScore()
begin
  record JALFileVc JFr,oldJFr;
  string 255 link;
  
  JFr.SerNr = StringToLongint(WebGetArg("sernr"));
  if readfirstmain(JFr,1,true) then begin
    if JFr.SMFCode == WebGetArg("code") then begin
      recordCopy(oldJFr,JFr);
      JFr.Score = StringToInt(WebGetArg("val"));
      if recordUpdate(oldJFr,JFr,true) then begin end;
    end;
  end;

  link = "/WebShowDocuments.hal";

  if nonblank(link) then begin
    ShowRedirectHtml(FormatSimpleLink(link,true)&"?code=" & WebGetArg("code") & "&user=" & WebGetArg("user"),false);
  end;

return;
end;