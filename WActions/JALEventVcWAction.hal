remote updating function Boolean FindEventText(record JALEventVc,var record JALEventTextVc);
external inner procedure PasteFromSysList(Integer,Integer);

global
procedure NewJALEventTranslationDsm()
begin
  record JALEventVc JEr;
  record WebNGTranslateVc WebNGTranslater;
  row WebNGTranslateVc WebNGTranslaterw;
  Integer wn;
  
  wn = CurWindow;
  GetWindowRecord(wn,JEr);
  RecordNew(WebNGTranslater);
  WebNGTranslater.Code = JEr.SerNr;
  WebNGTranslater.Comment = JEr.Comment;
  WebNGTranslaterw.OverviewText = JEr.Comment;
  MatRowPut(WebNGTranslater,0,WebNGTranslaterw);
  WebNGTranslater.FileName = 5;
  wn = OpenWindow("WebNGTranslateDClass",1,0,"","",WebNGTranslater);    
  return;
end;

global
updating procedure NewJALEventTextDsm()
begin
  Integer wn;
  record JALEventVc JEr;
  record JALEventTextVc JETr;

  wn = CurWindow;
  if (WindowState(wn)==Rs_Normal) then begin
    GetWindowRecord(wn,JEr);
    if (FindEventText(JEr,JETr)) then begin
      OpenWindow("JALEventTextDClass",1,0,"","",JETr);
    end;
  end;

  return;
end;

global
function Boolean JALEventDClassUpdateWindowSubset(Integer wn,var string subsetname)
begin
  record JALEventVc JEr;
  
  GetWindowRecord(wn,JEr);
  subsetname = JEr.SerNr;
  JALEventDClassUpdateWindowSubset = true;
  RETURN;
END;

global
function Boolean JALEventDClassListDblClick(Integer wn)
begin
  Integer mwn;
  record WebNGTranslateVc WebNGTranslater;
  
  if (WindowState(wn)==0) then begin
    if (ReadMarkedRecord(wn,WebNGTranslater)) then begin
      mwn = OpenWindow("WebNGTranslateDClass",1,0,"","",WebNGTranslater);
    end;
  end;
  JALEventDClassListDblClick = true;
  return;
end;

global
procedure OpenJALEventTranslationDsm()
begin
  Integer mwn,wn;
  record WebNGTranslateVc WebNGTranslater;
  
  wn = CurWindow;
  if (WindowState(wn)==0) then begin
    if (ReadMarkedRecord(wn,WebNGTranslater)) then begin
      mwn = OpenWindow("WebNGTranslateDClass",1,0,"","",WebNGTranslater);
    end;
  end;
return;
end;

global
updating function Boolean JALEventDClassArtCodeEFAfter(Integer wn,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;
  record JALEventFieldVc JFr;
  record JALEventVc JEr;
  row JALEventVc JErw;
  
  GetWindowRecord(wn,JEr);
  MatRowGet(JEr,rownr,JErw);
  JFr.SerNr = JErw.Field;
  if readfirstmain(JFr,1,true) then begin
    JErw.Comment = JFr.Comment;
    if (JFr.Req==1) then begin//messed up logic because in the StrSet req. means 0
      JErw.Req = 0;
    end else begin
      JErw.Req = 1;
    end;
    MatRowPut(JEr,rownr,JErw);
  end;

  PutWindowRecord(wn,JEr);

JALEventDClassArtCodeEFAfter = res;
return;
end;

global
updating function Boolean JALEventDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
BEGIN
  Boolean res;

  switch (fieldname) begin
    case "Field": res = JALEventDClassArtCodeEFAfter(wn,fn,rownr,changed);
  end;

  JALEventDClassAfterEditField = res;
return;
end;

global
procedure JALEventDClassButtonFlags(Integer wn,var Boolean duplOK,var Boolean newOK,var Boolean saveOK,var Boolean restOK)
begin
  saveOK = true;
  return;
end;

global
function Boolean JALSemesterSClassOnOpenWindow(Integer wn)
begin
  record JALSemesterBlock JSb;
  row JALSemesterBlock JSbrw;
  Integer rwcnt,i;
  
  BlockLoad(JSb);
  rwcnt = MatRowCnt(JSb);
  for (i=0; i<rwcnt; i=i+1) begin
    MatRowGet(JSb,i,JSbrw);
    SetListString(wn,BytesInString(JSbrw.YearStr),JSbrw.YearStr,false);
  end;

  JALSemesterSClassOnOpenWindow = false;
  return;
end;


global
function Boolean JALSemesterSClassOnOKWindow(Integer wn)
begin
  PasteFromSysList(wn,20);
  JALSemesterSClassOnOKWindow = false;
  return;
end;
